
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.first'
version = '0.0.1-SNAPSHOT'


java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def frontendDir = "$projectDir/src/main/reactboard"


// React 빌드 파일 삭제
task cleanReactBuild(type: Delete) {
	delete "$projectDir/src/main/resources/static/*"
}

// React 설치
task installReact(type: Exec) {
	workingDir frontendDir
	group = "build"
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine 'cmd', '/c', 'npm install'
	} else {
		commandLine 'npm', 'install'
	}
}

// React 빌드
task buildReact(type: Exec) {
	dependsOn installReact
	workingDir frontendDir
	group = "build"
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		commandLine 'cmd', '/c', 'npm run build'
	} else {
		commandLine 'npm', 'run', 'build'
	}
}

// React 빌드 파일 복사
task copyReactBuildFiles(type: Copy) {
	dependsOn buildReact
	from "$frontendDir/build/"
	into "$projectDir/src/main/resources/static"
}

// processResources task에 연결
tasks.named("processResources") {
	dependsOn copyReactBuildFiles
}

// bootRun task에 연결
tasks.named("bootRun") {
	dependsOn copyReactBuildFiles
}

tasks.named("build") {
	dependsOn copyReactBuildFiles
}
